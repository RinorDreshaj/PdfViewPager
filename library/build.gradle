apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

ext {
    libraryName = 'PdfViewPager'
    libraryDescription = 'Android widget to display PDF documents in your Views, Activities or Fragments.'
    siteUrl = 'https://github.com/voghDev/PdfViewPager'
    gitUrl = 'https://github.com/voghDev/PdfViewPager.git'
    libraryVersion = '1.1.2'
    developerId = 'voghDev'
    developerName = 'Olmo Gallegos'
    developerEmail = 'vendofiatpunto@hotmail.com'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    namespace "es.voghdev.pdfviewpager.library"
    compileSdk 34

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.register('checkstyle', Checkstyle) {
    configFile = rootProject.file('config/checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
}

if (project.hasProperty("android")) { // Android libraries
    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

} else {
    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }
}


artifacts {
    archives tasks.named('sourcesJar')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
